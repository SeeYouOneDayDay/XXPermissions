apply plugin: 'com.android.application'

android {
    // 修改31版本,可以直接查看关联的31源码
    //32 查看不了源码: https://raw.githubusercontent.com/hhhaiai/Picture/main/img/202204021808620.png
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.hjq.permissions.demo"
        minSdkVersion 14
        targetSdkVersion 32
        versionCode 1360
        versionName "13.6"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }


    // Apk 签名的那些事：https://www.jianshu.com/p/a1f8e5896aa2
    signingConfigs {
        config {
            storeFile file(StoreFile)
            storePassword StorePassword
            keyAlias KeyAlias
            keyPassword KeyPassword
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    applicationVariants.all { variant ->
        // apk 输出文件名配置
        variant.outputs.all { output ->
            outputFileName = rootProject.getName() + '.apk'
        }
    }
    dexOptions {
        preDexLibraries false
        maxProcessCount 8
        javaMaxHeapSize "4g"
    }
    // 使用 JDK 1.8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    aaptOptions {
        cruncherEnabled = false
        useNewCruncher = false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        checkReleaseBuilds true
        quiet true
        abortOnError false
        ignoreWarnings true
//        disable "UnusedResources", 'RestrictedApi', 'TypographyFractions','TypographyQuotes'
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        textOutput "stdout"
        textReport false
        check 'NewApi', 'InlinedApi'
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

dependencies {
    // 依赖 libs 目录下所有的 jar 和 aar 包
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    implementation project(':library')

    // 谷歌兼容库：https://developer.android.google.cn/jetpack/androidx/releases/appcompat?hl=zh-cn
    // noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:28.0.0'

    // 吐司框架：https://github.com/getActivity/ToastUtils
    implementation 'com.github.getActivity:ToastUtils:10.3'

    // 内存泄漏检测：https://github.com/square/leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'
}